; INPUT
;
; v0: vertex 		(x0, y0, z0, w = 1)
; v1: texcoord 		(tx0, ty0)
; v2: color 		
;
; v3: vertex 		(x1, y1, z1, w = 1)
; v4: texcoord		(tx1, ty1)
; v5: color 		

; OUTPUT
.out 	outpos 	position
.out 	outtex  texcoord0
.out	outcol	color

; Uniforms

.gsh
.entry gmain
.proc gmain

	; Emit the standard:
	;    x0,y0,tx0,ty0     x1,y0,tx1,ty0
	;                 +---+
	;                 |  /|
	;                 | / |
	;                 |/  |
	;                 +---+
	;    x0,y1,tx0,ty1     x1,y1,tx1,ty1
	;
	setemit 0							; top-left
	mov 	outpos.xz, 	v0.xz
	mov		outpos.y,   v0.y
	mov 	outtex.x, 	v1.x
	mov 	outtex.y, 	v1.y
	mov		outcol,		v2
	emit 

	setemit 1							; top-right
	mov 	outpos.xz, 	v3.xz
	mov		outpos.y,   v0.y
	mov 	outtex.x, 	v4.x
	mov 	outtex.y, 	v1.y
	mov		outcol,		v2
	emit 
	
	setemit 2, prim						; bottom-left
	mov 	outpos.xz, 	v0.xz
	mov		outpos.y,   v3.y
	mov 	outtex.x, 	v1.x
	mov 	outtex.y, 	v4.y
	mov		outcol,		v2
	emit 

	setemit 0, prim	inv					; bottom-right
	mov 	outpos.xz, 	v3.xz
	mov		outpos.y,   v3.y
	mov 	outtex.x, 	v4.x
	mov 	outtex.y, 	v4.y
	mov		outcol,		v2
	emit 

	nop
	end
	nop


.end